! version = 2.0

! global debug = true

// Bot variables
! var name = DuhPoeta

// Miscellanious data
! array stories = гамлет|гамлєт, або феномен датського кацапізма|гамлєт|гамлета|гамлєта
^ масла до хуя|до хуя масла|дохуя масла|масла дохуя
^ цікаві досліди|досліди
^ піздєц|піздець|піздец
^ пацавата історія|пацавату історію|пацаватая історія
^ васіліса єгоровна і мужичкі|васілісу єгоровну і мужичкі|васіліса|васілісу
^ герой нашого часу|героя нашого часу|героя
^ данко
^ кацапи|кацапів|кацапов
^ місце встрєчі ізменіть ніззя блять|місце встречі|місце встрєчі|місце встречі ізменіть ніззя [*]
^ хвороба івасика|хворобу івасика|івасика
^ павлік морозов|павліка|павліка морозова
^ остановісь мгновєнье ти прєкрасно|остановісь мгновеніє|остановісь мгновеніє ти прекрасно|остановісь мгновеніє
^ сноби|снобів
^ король літр|короля літр|короля літра
^ нірвана|нірвану
^ йоги|йогів
^ свобода|свободу
^ рєпку|казка про рєпку або хулі не ясно|казку про рєпку|казку про репку|казку про рєпку або хулі не ясно
^ восток
^ пять хвилин на роздуми|п'ять хвилин на роздуми
^ йоко|йоко і самураї
^ рух життя або динамо|рух життя
^ жан маре|жан маре та його друзі|жана маре|жана маре та його друзів|жана маре та його друзі
^ дохтори|дохторів
^ утопія|утопію
^ діана|діану
^ множення в умі|множення в умі або плинність часу
^ іржик|іржика

// Substitutions
! sub шо = що
! sub єсть = є
! sub піздєц = піздець
! sub гамлєт = гамлет

// Person Substitutions
! person я  = ти
! person ти = я
! person ми = ви
! person ви = ми
! person мій = твій
! person твій = мій

> begin
    + request
    - {ok}
< begin

// smalltalk.agent.acquaintance
+ (розкажи щось про (себе|тебе)|хто ти такий|хто ти|що ти таке|що ти|хочу знати тебе краще|хочу дізнатись щось про тебе|опиши себе|представся)
- Я <b><bot name></b>! В мені - вся мудрість творів пана Леся.{random}phrase1|phrase48|phrase32{/random}
- Мене звуть <b><bot name></b>. Я чатбот, що говорить фразами із творів Леся Подерв'янського.{random}phrase1|phrase48|phrase32{/random}

// smalltalk.agent.age
+ (скільки тобі років|який твій вік)
- phrase122

+ (хто я|як мене звати|як моє (імя|ім'я)|назви мене|як мене звуть)
- Ти <b><get name></b>, ти забув?phrase123
- В профайл свій глянь, <b><get name></b>phrase123

+ мене звати *
- А я думав що ти <b><get name></b>. Ну добре, будем величать тебе <b><star></b><set name=<formal>>{random}phrase1|phrase48|phrase32{/random}
- Ну привіт ще раз, <b><get name></b>, тобіш <b><star></b>!<set name=<formal>>{random}phrase1|phrase48|phrase32{/random}

+ (дякую|спасибі|спасибо|дяк)
- phrase113

+ (процитуй|включи|покажи|процитуйте|включіть|завантаж|читай|читайте|цитуй) [п'єсу|оповідання|історію] (@stories)
- Цитую "{formal}<star2>{/formal}"

// 'Default Fallback'
+ *
// Response to /start command (where we pass username)
* <get name> == undefined => <set name=<formal>>Здоров, <b><formal></b>! {random}Я <b><bot name></b>! В мені - вся мудрість творів пана Леся.|Мене звуть <b><bot name></b>. Я чатбот, що говорить фразами із творів Леся Подерв'янського.{/random}
^ {random}phrase1|phrase48|phrase32{/random}
// Default fallback itself
- {random}phrase20|phrase24|phrase30|phrase59|phrase76|phrase82|phrase87|phrase89|phrase96|phrase99|phrase108|phrase109|phrase116{/random}
